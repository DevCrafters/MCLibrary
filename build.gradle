buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id "de.undercouch.download" version "3.2.0"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

import org.apache.tools.ant.filters.ReplaceTokens
import de.undercouch.gradle.tasks.download.Download

group 'kr.rvs.mclibrary'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

repositories {
    mavenCentral()
    /* Spigot Repo */
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    /* Vault Repo */
    maven { url 'http://nexus.hc.to/content/repositories/pub_releases' }
    /* ProtocolLib */
    maven { url 'http://repo.dmulloy2.net/content/groups/public/' }
}

dependencies {
    compile('org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT') {
        exclude module: 'bungeecord-chat'
    }
    compile('net.milkbowl.vault:VaultAPI:1.6') {
        exclude module: 'bukkit'
    }
    compile group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.4.0-SNAPSHOT'
//    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile fileTree(dir: 'libs', include: '*.jar')

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0'
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "version"    : project.version,
            "pluginName" : rootProject.name,
            "mainPackage": project.group,
            "author"     : "EntryPoint"
    ]
}

shadowJar {
    dependencies {
        include dependency('com.google.code.gson:gson')
        relocate 'com.google.gson', 'kr.rvs.mclibrary.gson'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
//    archives shadowJar
    archives sourcesJar
    archives javadocJar
}
